; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{C4E2A613-7846-4648-B733-2E472C798E61}
AppName=金字塔财务管理工具
AppVersion=1.0
;AppVerName=金字塔财务管理工具 1.0
AppPublisher=StoneAnt, Inc.
DefaultDirName={pf}\PyramidTool
DefaultGroupName=金字塔财务管理工具
AllowNoIcons=yes
OutputBaseFilename=金字塔财务管理工具
SetupIconFile=D:\WorkSpaces\VisualStudio\WPF\WpfApplication3\Resources\Pyramid_Logo_white_128x128.ico
Password=123123
Compression=lzma
SolidCompression=yes

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked
Name: "quicklaunchicon"; Description: "{cm:CreateQuickLaunchIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked; OnlyBelowVersion: 0,6.1

[Files]
Source: "D:\WorkSpaces\VisualStudio\WPF\WpfApplication3\bin\Release\金字塔.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "D:\WorkSpaces\VisualStudio\WPF\WpfApplication3\bin\Release\templt.xls"; DestDir: "{app}"; Flags: ignoreversion
Source: "D:\WorkSpaces\VisualStudio\WPF\WpfApplication3\bin\Release\System.Data.SQLite.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "D:\WorkSpaces\VisualStudio\WPF\WpfApplication3\bin\Release\Data\*"; DestDir: "{app}\\Data"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\金字塔财务管理工具"; Filename: "{app}\金字塔.exe"
Name: "{group}\{cm:UninstallProgram,金字塔财务管理工具}"; Filename: "{uninstallexe}"
Name: "{commondesktop}\金字塔财务管理工具"; Filename: "{app}\金字塔.exe"; Tasks: desktopicon
Name: "{userappdata}\Microsoft\Internet Explorer\Quick Launch\金字塔财务管理工具"; Filename: "{app}\金字塔.exe"; Tasks: quicklaunchicon

[Run]
Filename: "{app}\金字塔.exe"; Description: "{cm:LaunchProgram,金字塔财务管理工具}"; Flags: nowait postinstall skipifsilent

[Code]
function IsDotNetDetected(version: string; service: cardinal): boolean;
// Indicates whether the specified version and service pack of the .NET Framework is installed.
//
// version -- Specify one of these strings for the required .NET Framework version:
//    'v1.1.4322'     .NET Framework 1.1
//    'v2.0.50727'    .NET Framework 2.0
//    'v3.0'          .NET Framework 3.0
//    'v3.5'          .NET Framework 3.5
//    'v4\Client'     .NET Framework 4.0 Client Profile
//    'v4\Full'       .NET Framework 4.0 Full Installation
//    'v4.5'          .NET Framework 4.5
//
// service -- Specify any non-negative integer for the required service pack level:
//    0               No service packs required
//    1, 2, etc.      Service pack 1, 2, etc. required
var
    key: string;
    install, release, serviceCount: cardinal;
    check45, success: boolean;
begin
    // .NET 4.5 installs as update to .NET 4.0 Full
    if version = 'v4.5' then begin
        version := 'v4\Full';
        check45 := true;
    end else
        check45 := false;

    // installation key group for all .NET versions
    key := 'SOFTWARE\Microsoft\NET Framework Setup\NDP\' + version;

    // .NET 3.0 uses value InstallSuccess in subkey Setup
    if Pos('v3.0', version) = 1 then begin
        success := RegQueryDWordValue(HKLM, key + '\Setup', 'InstallSuccess', install);
    end else begin
        success := RegQueryDWordValue(HKLM, key, 'Install', install);
    end;

    // .NET 4.0/4.5 uses value Servicing instead of SP
    if Pos('v4', version) = 1 then begin
        success := success and RegQueryDWordValue(HKLM, key, 'Servicing', serviceCount);
    end else begin
        success := success and RegQueryDWordValue(HKLM, key, 'SP', serviceCount);
    end;

    // .NET 4.5 uses additional value Release
    if check45 then begin
        success := success and RegQueryDWordValue(HKLM, key, 'Release', release);
        success := success and (release >= 378389);
    end;

    result := success and (install = 1) and (serviceCount >= service);
end;


function InitializeSetup(): Boolean;
begin
    if not IsDotNetDetected('v4\Full', 0) then begin
        MsgBox('金字塔财务管理工具 requires Microsoft .NET Framework 4.0 Full Profile.'#13#13
            'Please use Windows Update to install this version,'#13
            'and then re-run the 金字塔财务管理工具 setup program.', mbInformation, MB_OK);
        result := false;
    end else
        result := true;
end;